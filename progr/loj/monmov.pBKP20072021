/*
#1 TP 28831445 - 14.01.19
*/
{admcab.i}

def var aux-mondat  like monmov.mondat.
def var aux-moncod  like monmov.moncod.
def var aux-montcod like monmov.montcod.
def var aux-emite   like monmov.emite.
def var aux-monser  like monmov.monser.
def var aux-monnot  like monmov.monnot.

def var vqtd-promon like movim.movqtm.
def buffer cmonmov for monmov.
def buffer b-matriz-plani for plani.
def buffer b-matriz-movim for movim.

def var vok as log.
def var vsai as log init no.
def buffer bestab      for estab.
def var recatu1         as recid.
def var recatu2         as recid.
def var reccont         as int.
def var esqpos1         as int.
def var esqpos2         as int.
def var esqregua        as log.
def var esqvazio        as log.
def var esqascend       as log initial yes.
def var esqcom1         as char format "x(12)" extent 5
    initial [" Inclusao "," Alteracao "," Consulta "," Procura ","Montadores"].
def var esqcom2         as char format "x(12)" extent 5.
def var esqhel1         as char format "x(80)" extent 5
    initial [" Inclusao     de Montagem ",
             " Alteracao    de Montagem ",
             " Cancelamento de Montagem ",
             " Consulta     de Montagem ",
             " "].
def var esqhel2         as char format "x(12)" extent 5.

def var vetbcod-nf like monmov.etbcod.
def var vnumero-nf as integer.
def var vprocod-nf like monmov.procod.
def var vmovqtm as int.
def var vcont as int.

def temp-table tt-monmov like monmov.
def buffer bmonmov       for monmov.

form
    esqcom1
    with frame f-com1
                 row 4 no-box no-labels column 1 centered.
form
    esqcom2
    with frame f-com2
                 row screen-lines no-box no-labels column 1 centered.
assign
    esqregua = yes
    esqpos1  = 1
    esqpos2  = 1.

bl-princ:
repeat:
    disp esqcom1 with frame f-com1.
    disp esqcom2 with frame f-com2.
    if recatu1 = ?
    then run leitura (input "pri").
    else find monmov where recid(monmov) = recatu1 no-lock.

    if not available monmov
    then esqvazio = yes.
    else esqvazio = no.
    clear frame frame-a all no-pause.
    if not esqvazio
    then do:
        run frame-a.
    end.

    recatu1 = recid(monmov).
    if esqregua
    then color display message esqcom1[esqpos1] with frame f-com1.
    else color display message esqcom2[esqpos2] with frame f-com2.
    if not esqvazio
    then repeat:
        run leitura (input "seg").
        if not available monmov
        then leave.
        if frame-line(frame-a) = frame-down(frame-a)
        then leave.
        down
            with frame frame-a.
        run frame-a.
    end.
    if not esqvazio
    then up frame-line(frame-a) - 1 with frame frame-a.

    repeat with frame frame-a:

        if not esqvazio
        then do:
            find monmov where recid(monmov) = recatu1 no-lock.

            status default
                if esqregua
                then esqhel1[esqpos1] + if esqpos1 > 1 and
                                           esqhel1[esqpos1] <> ""
                                        then  string(monmov.moncod)
                                        else ""
                else esqhel2[esqpos2] + if esqhel2[esqpos2] <> ""
                                        then string(monmov.moncod)
                                        else "".
            run color-message.
            choose field
                    monmov.etbcod 
                    adm.montagem.monnom 
                    adm.montador.montnom 
                    monmov.monnot 
                    monmov.monser
                    monmov.procod 
                    monmov.movpc help ""
                go-on(cursor-down cursor-up
                      cursor-left cursor-right
                      page-down   page-up
                      tab PF4 F4 ESC return) /*color white/black*/ .
            run color-normal.
            status default "".

        end.
            if keyfunction(lastkey) = "TAB"
            then do:
                if esqregua
                then do:
                    color display normal esqcom1[esqpos1] with frame f-com1.
                    color display message esqcom2[esqpos2] with frame f-com2.
                end.
                else do:
                    color display normal esqcom2[esqpos2] with frame f-com2.
                    color display message esqcom1[esqpos1] with frame f-com1.
                end.
                esqregua = not esqregua.
            end.
            if keyfunction(lastkey) = "cursor-right"
            then do:
                if esqregua
                then do:
                    color display normal esqcom1[esqpos1] with frame f-com1.
                    esqpos1 = if esqpos1 = 5 then 5 else esqpos1 + 1.
                    color display messages esqcom1[esqpos1] with frame f-com1.
                end.
                else do:
                    color display normal esqcom2[esqpos2] with frame f-com2.
                    esqpos2 = if esqpos2 = 5 then 5 else esqpos2 + 1.
                    color display messages esqcom2[esqpos2] with frame f-com2.
                end.
                next.
            end.
            if keyfunction(lastkey) = "cursor-left"
            then do:
                if esqregua
                then do:
                    color display normal esqcom1[esqpos1] with frame f-com1.
                    esqpos1 = if esqpos1 = 1 then 1 else esqpos1 - 1.
                    color display messages esqcom1[esqpos1] with frame f-com1.
                end.
                else do:
                    color display normal esqcom2[esqpos2] with frame f-com2.
                    esqpos2 = if esqpos2 = 1 then 1 else esqpos2 - 1.
                    color display messages esqcom2[esqpos2] with frame f-com2.
                end.
                next.
            end.
            if keyfunction(lastkey) = "page-down"
            then do:
                do reccont = 1 to frame-down(frame-a):
                    run leitura (input "down").
                    if not avail monmov
                    then leave.
                    recatu1 = recid(monmov).
                end.
                leave.
            end.
            if keyfunction(lastkey) = "page-up"
            then do:
                do reccont = 1 to frame-down(frame-a):
                    run leitura (input "up").
                    if not avail monmov
                    then leave.
                    recatu1 = recid(monmov).
                end.
                leave.
            end.
            if keyfunction(lastkey) = "cursor-down"
            then do:
                run leitura (input "down").
                if not avail monmov
                then next.
                color display white/red 
                         monmov.etbcod 
                         adm.montagem.monnom 
                         adm.montador.montnom 
                         monmov.monnot 
                         monmov.monser
                         monmov.procod 
                         monmov.movpc                
                         with frame frame-a.
                if frame-line(frame-a) = frame-down(frame-a)
                then scroll with frame frame-a.
                else down with frame frame-a.
            end.
            if keyfunction(lastkey) = "cursor-up"
            then do:
                run leitura (input "up").
                if not avail monmov
                then next.
                color display white/red monmov.etbcod 
                    adm.montagem.monnom 
                    adm.montador.montnom 
                    monmov.monnot 
                    monmov.monser
                    monmov.procod 
                    monmov.movpc with frame frame-a.
                if frame-line(frame-a) = 1
                then scroll down with frame frame-a.
                else up with frame frame-a.
            end.
 
        if keyfunction(lastkey) = "end-error"
        then leave bl-princ.

        if keyfunction(lastkey) = "return" or esqvazio
        then do:                     
            form tt-monmov.etbcod label "Filial..."     space(2)
                 estab.etbnom      no-label
                 skip
                 tt-monmov.moncod label "Montagem." format ">>>>>>>9" space(2)
                 adm.montagem.monnom no-label
                 skip
                 tt-monmov.montcod label "Montador."
                 adm.montador.montnom no-label
                 skip
                 tt-monmov.emite   label "Loja NF.." 
                 skip
                 tt-monmov.monnot  label "Numero NF" space(2)
                 tt-monmov.monser  label "Serie NF"
                 tt-monmov.mondat  label "Dt.Montagem" colon 56
                 skip
                 tt-monmov.procod  label "Produto.."
                 tt-monmov.movpc   label "Valor..."  format ">>>,>>9.99"
                 vmovqtm label "Quantidade"    colon 56
                 with frame f-monmov-i color black/cyan
                      centered side-label row 5 .

            form monmov.etbcod label "Filial..." space(2)
                 estab.etbnom      no-label
                 skip
                 monmov.moncod label "Montagem." format ">>>>>>>9"
                 space(2)
                 adm.montagem.monnom no-label
                 skip
                 monmov.montcod label "Montador."
                 adm.montador.montnom no-label
                 skip
                 monmov.emite   label "Loja NF.."
                 skip
                 monmov.monnot  label "Numero NF" space(2)
                 monmov.monser  label "Serie NF"
                 monmov.mondat  label "Dt.Montagem"
                 skip
                 monmov.procod  label "Produto.."
                 monmov.movpc   label "Valor..."
                 with frame f-monmov color black/cyan
                      centered side-label row 5.

            hide frame frame-a no-pause.
            
            if esqregua
            then do:
                display caps(esqcom1[esqpos1]) @ esqcom1[esqpos1]
                        with frame f-com1.

                if esqcom1[esqpos1] = "Montadores" 
                then do with frame f-cons-mont.
                    hide frame f-com1 no-pause.
                    hide frame f-com2 no-pause.
                    hide frame frame-a no-pause.
                    
                    run cons-mont.p.
                    
                    view frame f-com1. pause 0.
                    view frame f-com2. pause 0.
                    view frame frame-a. pause 0. 
                end.
                
                if esqcom1[esqpos1] = " Procura " 
                then do with frame f-procura centered overlay
                            side-labels row 8 on error undo.
                    
                    view frame frame-a. pause 0.
                    disp setbcod label "Loja NF..".
                    update vnumero-nf label "Numero NF" format ">>>>>>9" 
                           vprocod-nf label "Produto..".

                    find first bmonmov where 
                               bmonmov.etbcod = setbcod and
                               bmonmov.monnot = vnumero-nf and
                               bmonmov.procod = vprocod-nf 
                                                         no-lock no-error.
                    if not avail bmonmov 
                    then do:
                        message "Montagem nao encontrada.". pause 1.  
                        undo. 
                    end. 
                    hide frame f-procura.
                    recatu1 = recid(bmonmov). 
                    leave.
                end.
                
                if esqcom1[esqpos1] = " Inclusao " or esqvazio
                then do with frame f-monmov-i 
                    color black/cyan centered side-label row 5 on error undo.
                    
                 assign aux-mondat  = ? 
                        aux-moncod  = 0 
                        aux-montcod = 0 
                        aux-emite   = 0 
                        aux-monser  = "" 
                        aux-monnot  = 0.

                 repeat with frame f-monmov-i:
                   
                    for each tt-monmov. delete tt-monmov. end.
                    vsai = no.
                    create tt-monmov.
                    assign tt-monmov.mondat  = today
                           tt-monmov.etbcod  = setbcod
                           tt-monmov.moncod  = aux-moncod
                           tt-monmov.montcod = aux-montcod 
                           tt-monmov.emite   = aux-emite
                           tt-monmov.monser  = aux-monser
                           tt-monmov.monnot  = aux-monnot.

                    find estab where estab.etbcod = tt-monmov.etbcod
                         no-lock.

                    disp tt-monmov.mondat skip
                         tt-monmov.etbcod
                         estab.etbnom no-label skip.
                    
                    update tt-monmov.moncod format ">>>>>>>9".

                    find adm.montagem where
                         adm.montagem.moncod = tt-monmov.moncod and
                         adm.montagem.situacao
                         no-lock no-error.
                    if not avail adm.montagem
                    then do:
                        message color red/with
                            "Tipo de Montagem nao cadastrada ou desativada."
                            view-as alert-box.
                        undo.
                    end.
                    else disp adm.montagem.monnom no-label skip.
                           
                    find first monper  where
                               monper.etbcod = setbcod and
                               monper.moncod = tt-monmov.moncod and
                               monper.situacao
                               no-lock no-error.
                    if not avail monper
                    then do:
                        message color red/with
                            "Tipo de Montagem indisponivel para filial"
                            view-as alert-box.
                        undo.
                    end.           

                    update tt-monmov.montcod.
                    find adm.montador where 
                         adm.montador.montcod  = tt-monmov.montcod and
                         adm.montador.situacao
                         no-lock no-error.
                    if not avail adm.montador
                    then do:
                        message "Montador nao cadastrado".
                        undo.
                    end.
                    else disp adm.montador.montnom no-label skip.
                    
                    if tt-monmov.moncod = 1 
                    then do:
                        tt-monmov.monser = "V".

                        update tt-monmov.emite   skip.
                        
                        find bestab where
                             bestab.etbcod = tt-monmov.emite
                             no-lock no-error.
                        if not avail bestab
                        then do:
                            message "Filial nao cadastrada.".
                            undo.
                        end.
                        
                        update tt-monmov.monnot  skip
                               tt-monmov.monser  skip
                               tt-monmov.procod  skip.
                    end.
                    else do:
                        tt-monmov.emite = tt-monmov.etbcod.
                        update tt-monmov.procod  skip.
                    end.
                    
                    /*** Validando ***/
                    find produ where produ.procod = tt-monmov.procod
                                                  no-lock no-error.
                    if not avail produ
                    then do:
                        message "Produto nao cadastrado".
                        undo.
                    end.
                    if produ.protam = "NAO" or
                       produ.protam = ""
                    then do:
                        message "Produto nao esta cadastrado p/ Montagem".
                        undo.
                    end.
                    disp produ.pronom.
                 /* retirado no dia 05/06/2014 e incluido novamente no dia 08/03/2015 - solicitado pelo RH */
                    
                    if adm.montagem.moncod = 2 and setbcod <> 65
                    then do:
                        if produ.catcod = 31
                        then
                        find first produaux where
                         produaux.nome_campo  = "Mix" and
                         produaux.valor_campo = string(setbcod,"999") + ",Sim"
                         no-lock no-error.
                        if avail produaux
                        then do:
                            find first produaux where
                            produaux.procod      = produ.procod and
                            produaux.nome_campo  = "Mix" and
                            produaux.valor_campo = 
                                                string(setbcod,"999") + ",Sim"
                            no-lock no-error.
                        
                        if not avail produaux
                        then do:
                            message color red/with
                            "Não é possível incluir montagem para mostruário de produto fora do MIX." view-as alert-box.
                            undo.
                        end.
                    end.
                    
                    end.

                    /*****************/
                    
                    /***/
                    if tt-monmov.moncod = 1
                    then do:
                    
                    find last plani
                        where (plani.movtdc = 5 or plani.movtdc = 81) 
                          and plani.etbcod = tt-monmov.emite
                          and plani.emite  = tt-monmov.emite
                          and plani.serie  = tt-monmov.monser
                          and plani.numero = tt-monmov.monnot 
                                      no-lock no-error.
                    if avail plani
                    then do:
                        if (today - plani.pladat) > 90
                        then do:
                            message "Nota Fiscal emitida a mais de 3 meses.
                                    Continuar? "
                                    update sresp.
                            if not sresp
                            then undo.
                        end.
                        vqtd-promon = 0.
                        for each cmonmov where 
                                 cmonmov.etbcod = plani.etbcod and
                                 cmonmov.monnot = plani.numero and
                                 cmonmov.monser = plani.serie and
                                 cmonmov.procod = tt-monmov.procod
                                 no-lock.
                            vqtd-promon = vqtd-promon + 1.
                        end.         
                        vok = yes.
                        for each movim
                           where movim.etbcod = plani.etbcod
                             and movim.placod = plani.placod
                             and movim.movtdc = plani.movtdc
                             and movim.movdat = plani.pladat
                                                          no-lock:

                            if movim.procod = tt-monmov.procod  and
                               vqtd-promon < movim.movqtm
                            then do:
                                find first bmonmov where
                                        bmonmov.monnot = tt-monmov.monnot
                           /* #1 */ and bmonmov.etbcod = tt-monmov.etbcod
                                    and bmonmov.monser = tt-monmov.monser
                                    and bmonmov.procod = tt-monmov.procod
                                        no-lock no-error.
                                if avail bmonmov
                                then do:
                                    if movim.movqtm > 1
                                    then.
                                    else do:
                                        if bmonmov.procod = tt-monmov.procod
                                        then do:
                                            message "Montagem ja incluida".
                                            pause 3 no-message.
                                            vsai = yes.
                                            recatu1 = ?.
                                            leave.
                                        end.
                                    end.
                                end.        
                                
                                tt-monmov.movpc = movim.movpc.
                            end.
                            else do:
                                if movim.procod = tt-monmov.procod  
                                and vqtd-promon >= movim.movqtm
                                then do:
                                    message "Todas as quantidades do produto na Nota ja incluidas na montagem."         .
                                    vok = no.
                                end.
                            end.    
                        end.                 

                        if vsai 
                        then leave.
                        
                        if tt-monmov.movpc = 0
                        then do:
                            if vok = yes
                            then message "Produto nao encontrado na Nota.".
                            undo.
                        end.
                    end.
                    else do:
                        if adm.montagem.moncod = 1
                        then do:
                            message "Nota Fiscal nao encontrada.".
                            pause 3 no-message.
                            undo.
                        end.
                    end.
                    end.        
                    
                    if tt-monmov.moncod >= 2
                    then do:
                        if adm.montagem.moncod = 2 /* Loja */
                        then do:
                            find last movim where
                                      movim.procod = tt-monmov.procod   
                                  and movim.movdat >= (today - 45)
                                  no-lock no-error.
                            if not avail movim
                            then do:
                                 find first estoq where
                                            estoq.etbcod = tt-monmov.emite
                                        and estoq.procod = tt-monmov.procod
                                        and estoq.estatual > 0
                                          no-lock no-error.
                                 if not avail estoq
                                 then do:
                             message "Produto sem movimentacao (45 dias) e sem saldo".
                                    pause 3 no-message.
                                    undo.
                                 end.
                            end.
                        end.
                        
                        /*************************************************
                        Em montagens de tipo 2 deve pegar sempre o preço de
                        venda do produto e nao mais o ultimo valor vendido
                        **************************************************/

                        /*
                        find last movim where
                                movim.etbcod = tt-monmov.emite and
                                movim.procod = tt-monmov.procod
                              and movim.movtdc = 5
                              and movim.movdat <= today
                              no-lock no-error.
                        if avail movim
                        then tt-monmov.movpc = movim.movpc.
                        else do:
                        */
                            find first estoq where
                                            estoq.etbcod = tt-monmov.emite
                                        and estoq.procod = tt-monmov.procod
                                            no-lock no-error.
                                            
                            if estoq.estbaldat <> ?
                            then do:
                                 if estoq.estbaldat <= today and
                                    estoq.estprodat >= today 
                               then tt-monmov.movpc = estoq.estproper.
                               else tt-monmov.movpc = estoq.estvenda.
                            end.
                            else do:
                                 if estoq.estprodat <> ?
                                     and estoq.estprodat >= today
                              then tt-monmov.movpc = estoq.estproper.
                              else tt-monmov.movpc = estoq.estvenda.
                            end.
                        /*
                        end.
                        */
                        disp /*update*/ tt-monmov.movpc.
                    end.
                    /***/
                    disp tt-monmov.movpc.
                    
                    if tt-monmov.movpc = 0
                    then update tt-monmov.movpc.
                    
                    if tt-monmov.moncod = 1
                    then do:
                        find first b-matriz-plani
                             where b-matriz-plani.etbcod = tt-monmov.emite
                               and b-matriz-plani.emite  = tt-monmov.emite
                               and b-matriz-plani.serie  = tt-monmov.monser
                               and b-matriz-plani.numero = tt-monmov.monnot
                                                       no-lock no-error.
                        if avail b-matriz-plani
                        then
                        find first movim
                         where movim.etbcod = b-matriz-plani.etbcod
                           and movim.placod = b-matriz-plani.placod
                           and movim.movtdc = b-matriz-plani.movtdc
                           and movim.movdat = b-matriz-plani.pladat
                           and movim.procod = tt-monmov.procod
                                                          no-lock no-error.
                        if avail movim
                        then assign vmovqtm = movim.movqtm.
                        else assign vmovqtm = 1.
                        
                        bl_qtd_montagem:
                        repeat:
                            update vmovqtm with frame f-monmov-i.
                            if vmovqtm > movim.movqtm
                            then do:
                                message "A quantidade informada não pode ser "
                                        "maior que a quantidade "
                                        "vendida na NF informada!"
                                            view-as alert-box.
                                assign vmovqtm = movim.movqtm.
                                next bl_qtd_montagem.
                            end.
                            else leave bl_qtd_montagem.
                        end.
                    end.
                    else assign vmovqtm = 1.
                    
                    message "Confirma a inclusao de montagem?" update sresp.
                    if not sresp 
                    then do:
                        recatu1 = ?.
                        leave.
                    end.
                    else do:
                        vcont = 0.
                        do vcont = 1 to vmovqtm:
                            create monmov.
                            buffer-copy tt-monmov to monmov.
                            recatu1 = recid(monmov).
                        end.
                        leave.                
                    end.

                    if vsai 
                    then leave.
                    
                    message "Mais algum produto para mesma Nota Fiscal?"
                        update sresp.
                    if not sresp
                    then leave.
                    else
                        assign aux-mondat  = tt-monmov.mondat
                               aux-moncod  = tt-monmov.moncod 
                               aux-montcod = tt-monmov.montcod 
                               aux-emite   = tt-monmov.emite
                               aux-monser  = tt-monmov.monser 
                               aux-monnot  = tt-monmov.monnot.
                 END.
                end.
                
                if esqcom1[esqpos1] = " Consulta " or
                   esqcom1[esqpos1] = " Exclusao " or
                   esqcom1[esqpos1] = " Alteracao "
                then do with frame f-monmov.
                    find estab where estab.etbcod = monmov.etbcod
                                        no-lock no-error.
                    find adm.montagem where
                         adm.montagem.moncod = monmov.moncod
                                                no-lock no-error.
                    find adm.montador where 
                         adm.montador.montcod = monmov.montcod
                                                no-lock no-error.
                
                    disp monmov.mondat.
                    disp estab.etbnom no-label.
                    disp adm.montagem.monnom no-label.
                    disp adm.montador.montnom no-label.
                    disp monmov except aux.
                end.
                
                if esqcom1[esqpos1] = " Alteracao "
                then do with frame f-monmov on error undo.
                    find monmov where recid(monmov) = recatu1 exclusive.
                    find estab where
                         estab.etbcod = monmov.etbcod no-lock no-error.
                    find adm.montagem where
                         adm.montagem.moncod = monmov.moncod
                                                no-lock no-error.
                    find adm.montador where 
                         adm.montador.montcod = monmov.montcod
                                                no-lock no-error.
                
                    disp monmov.mondat.
                    disp estab.etbnom no-label.
                    disp adm.montagem.monnom no-label.
                    disp adm.montador.montnom no-label.
                    disp monmov.etbcod.

                    find estab where estab.etbcod = monmov.etbcod
                                        no-lock no-error.
                    if not avail estab
                    then do:
                        message "Filial nao cadastrada.".
                        undo.
                    end.
                    else disp estab.etbnom no-label skip.
                    
                    update monmov.moncod format ">>>>>>>9".  
                    
                    find adm.montagem where
                         adm.montagem.moncod = monmov.moncod
                         and adm.montagem.situacao
                                                no-lock no-error.
                    if not avail adm.montagem
                    then do:
                        message color red/with
                            "Tipo de Montagem nao cadastrada ou desativada."
                            view-as alert-box.
                        undo.
                    end.
                    else disp adm.montagem.monnom no-label skip.
                           
                    update monmov.montcod.
                    
                    find adm.montador where 
                         adm.montador.montcod = monmov.montcod
                     and adm.montador.situacao = yes
                                                no-lock no-error.
                    if not avail adm.montador
                    then do:
                        message "Montador nao cadastrado".
                        undo.
                    end.
                    else disp adm.montador.montnom no-label skip.

                    update monmov.emite skip
                           monmov.monnot  skip
                           monmov.monser  skip
                           monmov.procod /* skip
                           monmov.movpc*/.
                end.
            end.
            else do:
                display caps(esqcom2[esqpos2]) @ esqcom2[esqpos2]
                        with frame f-com2.
                if esqcom2[esqpos2] = "  "
                then do:
                    hide frame f-com1  no-pause.
                    hide frame f-com2  no-pause.
                    /* run programa de relacionamento.p (input ). */
                    view frame f-com1.
                    view frame f-com2.
                end.
                leave.
            end.
        end.
        if not esqvazio
        then do:
            run frame-a.
        end.
        if esqregua
        then display esqcom1[esqpos1] with frame f-com1.
        else display esqcom2[esqpos2] with frame f-com2.
        recatu1 = recid(monmov).
    end.
    if keyfunction(lastkey) = "end-error"
    then do:
        view frame fc1.
        view frame fc2.
    end.
end.
hide frame f-com1  no-pause.
hide frame f-com2  no-pause.
hide frame frame-a no-pause.

procedure frame-a.

    find adm.montagem where
         adm.montagem.moncod = monmov.moncod no-lock no-error.
    find adm.montador where
         adm.montador.montcod = monmov.montcod no-lock no-error.
         
    display monmov.etbcod    column-label "Fil" format ">>9"
            adm.montagem.monnom  column-label "Montagem" format "x(17)"
            when avail adm.montagem
            adm.montador.montnom column-label "Montador" format "x(17)"
            when avail adm.montador
            monmov.monnot    column-label "Num. NF"
            monmov.monser    column-label "Serie"
            monmov.procod    column-label "Produto"
            monmov.movpc     column-label "Valor"
            with frame frame-a 11 down centered color white/red row 5.
            
end procedure.
procedure color-message.
    find adm.montagem where
         adm.montagem.moncod = monmov.moncod no-lock no-error.
    find adm.montador where
         adm.montador.montcod = monmov.montcod no-lock no-error.

    color display message
            monmov.etbcod    column-label "Fil" format ">>9"
            adm.montagem.monnom  column-label "Montagem" format "x(17)"
            when avail adm.montagem
            adm.montador.montnom column-label "Montador" format "x(17)"
            when avail adm.montador
            monmov.monnot    column-label "Num. NF"
            monmov.monser    column-label "Serie"
            monmov.procod    column-label "Produto"
            monmov.movpc     column-label "Valor"

            with frame frame-a.
end procedure.

procedure color-normal.
    find adm.montagem where
         adm.montagem.moncod = monmov.moncod no-lock no-error.
    find adm.montador where
         adm.montador.montcod = monmov.montcod no-lock no-error.

    color display normal
            monmov.etbcod    column-label "Fil" format ">>9"
            adm.montagem.monnom  column-label "Montagem" format "x(17)"
            when avail adm.montagem
            adm.montador.montnom column-label "Montador" format "x(17)"
            when avail adm.montador
            monmov.monnot    column-label "Num. NF"
            monmov.monser    column-label "Serie"
            monmov.procod    column-label "Produto"
            monmov.movpc     column-label "Valor"
            with frame frame-a.
end procedure.




procedure leitura . 
def input parameter par-tipo as char.
        
if par-tipo = "pri" 
then  
    if esqascend  
    then  
        find first monmov where
                   monmov.etbcod = setbcod and 
                   monmov.mondat >= today - 30
                   no-lock no-error.
    else  
        find last monmov  where
                  monmov.etbcod = setbcod and
                  monmov.mondat >= today - 30
                  no-lock no-error.
                                             
if par-tipo = "seg" or par-tipo = "down" 
then  
    if esqascend  
    then  
        find next monmov  where
                  monmov.etbcod = setbcod /*and
                  monmov.mondat >= today - 30*/
                  no-lock no-error.
    else  
        find prev monmov  where
                  monmov.etbcod = setbcod /*and
                  monmov.mondat >= today - 30 */
                  no-lock no-error.
             
if par-tipo = "up" 
then                  
    if esqascend   
    then   
        find prev monmov where
                  monmov.etbcod = setbcod /*and
                  monmov.mondat >= today - 30*/
                  no-lock no-error.
    else   
        find next monmov where 
                  monmov.etbcod = setbcod /*and
                  monmov.mondat >= today - 30  */
                  no-lock no-error.
        
end procedure.
         

