def input parameter par_props  as char.
def input parameter par_arqlog as char.

def var vpolitica   as char.
def var vidproposta as char.
def var vct  as int.
def var vtag as char.
def var vchave as char.
def var vvalor as char.

def var vRequest         as longchar.
def var vnamespace       as char init "neur:".
def var vResponse        as longchar.
def var lConectou        as log.

def SHARED temp-table tt-retorno no-undo
    field PARAMETRO as char format "x(30)"
    field RESPOSTA  as char format "x(40)"
    index par  PARAMETRO asc.


function GeraXml returns log
    (input a-acao  as char,
     input b-acao  as char,
     input p-tag   as char,
     input p-valor as char):

    if p-valor = ? then p-valor = "".

    if a-acao = "XML" and
       b-acao = ""
    then vRequest = vRequest + "<?xml version='1.0' encoding='UTF-8' ?>"
                + chr(10).

    else if a-acao <> "" and
            b-acao <> "" and
            p-valor = ""
    then vRequest = vRequest + "<" + vnamespace + p-tag + "/>" /*+ chr(10)*/.

    else if a-acao <> "" or
            b-acao <> ""
    then do:
        if a-acao <> ""
        then
            if a-acao = "TAG"
            then vRequest = vRequest + "<" + vnamespace + p-tag + ">".

        if p-valor <> ""
        then vRequest = vRequest + p-valor.

        if b-acao <> ""
        then do:
            if b-acao = "TAG"
            then vRequest = vRequest + "</" + vnamespace + p-tag + ">" +
                     chr(10).
        end.
    end.
end function.


procedure geralog.
    def input parameter p_mensagem as char.

    output to value(par_arqlog) append.
    put unformatted string(time, "hh:mm:ss") " WS " p_mensagem skip.
    output close.

end procedure.


procedure obtemNode.
    def input param vh as handle.

    def var hc   as handle.
    def var loop as int.

    create x-nodeRef hc.
    do loop = 1 to vh:num-children:
        vh:get-child(hc,loop).

        if hc:num-children > 0
        then run obtemnode (input hc:handle).

        if (vh:name = "Resultado" or
            vh:name = "CdOperacao" or
            vh:name = "DsMensagem" or
            vh:name begins "RET_" or
            vh:name = "VI_NEUROTECH_CD_OPERACAO" or
            vh:name = "PROP_IDOPERACAO") and
           hc:node-value <> ""
        then do:
            create tt-Retorno.
            assign
                tt-Retorno.parametro = vh:name
                tt-Retorno.resposta  = trim(string(hc:node-value)).
        end.
    end.
end procedure.


def var vNeurotechHost    as char.
def var vNeurotechConnect as char.

/*Handles WS*/
def var hWebService        as handle no-undo.
def var hWorkflowWebServiceSoap as handle no-undo.
def var hDoc               as handle.
def var hRoot              as handle.


PROCEDURE executarFluxoComParametros:
    DEFINE INPUT  PARAMETER parameters1 AS LONGCHAR NO-UNDO.
    DEFINE OUTPUT PARAMETER parameters2 AS LONGCHAR NO-UNDO.
END PROCEDURE.

run le_tabini.p (0, 0, "Neurotech_Host", OUTPUT vNeurotechHost).

vNeurotechConnect = "-WSDL " + vNeurotechHost + "?wsdl"
                   + " -ServiceNamespace http://neurotech.com.br/"
                   + " -nohostverify"
                   + " -SOAPEndpoint "         + vNeurotechHost.
     
run geralog("Conectando").
create server hWebService.
lConectou = hWebService:connect(vNeurotechConnect) no-error.
if not lConectou
then do:
    run geralog("Nao foi possível conectar ao servidor").
    return.
end.
        
RUN WorkflowWebServiceSoap SET hWorkflowWebServiceSoap ON hWebService.

if not valid-handle(hWorkflowWebServiceSoap)
then do:
    run geralog("Nao foi possivel conectar a porta hWorkflowWebServiceSoap").
    return.
end.

do vct = 1 to num-entries(par_props, "&").
    vtag = entry(vct, par_props, "&").

    if num-entries(vtag, "=") = 2
    then do.
        vchave = entry(1, vtag ,"=").
        vvalor = entry(2, vtag ,"=").
        if vchave = "POLITICA"
        then vpolitica = vvalor.
        else if vchave = "PROP_CPFCLI"
        then vidproposta = vvalor.
    end.
end.

geraxml("TAG","",
'executarFluxoComParametros ' +
'xmlns:soap="http://www.w3.org/2003/05/soap-envelope" ' +
'xmlns:neur="http://neurotech.com.br/"', "").

geraxml("TAG","","Credenciais", "").

geraxml("TAG","TAG","CodigoAssociado", "148").
geraxml("TAG","TAG","CodigoFilial", "0").
geraxml("TAG","TAG","Senha", "abcd@1234").

geraxml("","TAG","Credenciais", "").

geraxml("TAG","","Fluxo", "").

geraxml("TAG","TAG","NmPolitica", vpolitica).
geraxml("TAG","TAG","TagVersaoPolitica", "").
geraxml("TAG","TAG","NmFluxoResultado", "FLX_PRINCIPAL").
geraxml("TAG","TAG","IdProposta", vidproposta).

geraxml("TAG","","LsParametros", "").

do vct = 1 to num-entries(par_props, "&").
    vtag = entry(vct, par_props, "&").

    if num-entries(vtag, "=") = 2
    then do.
        vchave = entry(1, vtag ,"=").
        vvalor = entry(2, vtag ,"=").
        if vchave <> "POLITICA"
        then do.
            geraxml("TAG","","ParametroFluxo", "").
            geraxml("TAG","TAG","NmParametro", vchave).
            geraxml("TAG","TAG","VlParametro", left-trim(trim(vvalor))).
            geraxml("","TAG","ParametroFluxo", "").
        end.
    end.
end.

geraxml("","TAG","LsParametros", "").

geraxml("","TAG","Fluxo", "").

geraxml("TAG","","Parametros", "").

geraxml("TAG","","Propriedade", "").
geraxml("TAG","TAG","Nome", "USUARIO").
geraxml("TAG","TAG","Valor","USUARIO").
geraxml("","TAG","Propriedade", "").

geraxml("","TAG","Parametros", "").

geraxml("","TAG","executarFluxoComParametros", "").

output to value(par_arqlog) append.
put unformatted string(time, "hh:mm:ss") " Envio:" skip 
                string(vrequest).
output close.

RUN executarFluxoComParametros
            IN hWorkflowWebServiceSoap(INPUT vrequest, OUTPUT vResponse).

output to value(par_arqlog) append.
put unformatted string(time, "hh:mm:ss") " Resposta:" skip.
export vResponse.
put skip.
output close.

hWebService:disconnect().
delete object hWebService.

/* RETORNO */
create x-document hDoc.
Hdoc:load("LONGCHAR",vResponse,false).

create x-noderef hRoot.
hDoc:get-document-element(hRoot).

if error-status:error        or
   error-status:num-messages > 0
then do:
    run gera_log("Erro na leitura do XML de resposta").
    return.
end.

run obtemnode (input hRoot).

